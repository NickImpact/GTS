plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
}

repositories {
    mavenCentral()
    maven {
        name 'sponge-repo'
        url 'https://repo.spongepowered.org/repository/sponge-legacy'
    }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = "Github Packages - Protocol Control"
        url = "https://maven.pkg.github.com/ichorpowered/protocolcontrol"
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        name = "CodeMC"
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    compile project(':api')
    compile project(':common')
    compile project(':sponge')

    shadow 'net.impactdev.impactor:sponge:3.1.2'
    shadow 'org.spongepowered:spongeapi:7.3.0'
    shadow 'net.kyori:adventure-api:4.3.0'
    shadow 'net.kyori:adventure-text-serializer-legacy:4.3.0'
    shadow 'com.ichorpowered:protocolcontrol:0.0.3'
}

shadowJar {
    archiveName = "GTS-Sponge-${project.version}.jar"

    dependencies {
        include project(':api')
        include project(':common')
        include project(':sponge')
    }


    relocate ('net.kyori.adventure', 'net.impactdev.impactor.relocations.adventure')
    relocate ('net.kyori.examination', 'net.impactdev.impactor.relocations.examination')
    relocate ('com.github.benmanes.caffeine', 'net.impactdev.impactor.relocations.caffeine')
    relocate ('io.github.classgraph', 'net.impactdev.impactor.relocations.classgraph')
    relocate ('org.mariuszgromada.math.mxparser', 'net.impactdev.impactor.relocations.mxparser')
    relocate ('redis.clients.jedis', 'net.impactdev.impactor.relocations.jedis')
    relocate ('org.apache.commons.pool2', 'net.impactdev.impactor.relocations.commonspool2')
}

reobf {
    shadowJar {}
}

blossom {
    blossomSourceReplacementJava.output = file("$buildDir/sources/java")
    replaceToken '@version@', version
    replaceToken '@git_commit@', gitHash.commit
    replaceToken '@description@', 'A player controlled market system'
}

blossomSourceReplacementJava.dependsOn(gitHash)
jar.dependsOn(gitHash)
build.dependsOn reobfShadowJar