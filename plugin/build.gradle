repositories {
    mavenCentral()
    maven {
        name 'Sponge Releases'
        url 'https://repo.spongepowered.org/repository/maven-releases'
    }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven {
        name = "Github Packages - Protocol Control"
        url = "https://maven.pkg.github.com/ichorpowered/protocolcontrol"
        credentials {
            username = System.getenv("GITHUB_USERNAME")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        name = "CodeMC"
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    api project(':api')
    implementation project(':common')
    implementation project(':sponge')
    implementation project(':plugin:sponge-launcher')

    // Impactor
    implementation 'net.impactdev.impactor:launcher:4.0.0-SNAPSHOT'

    // Sponge
    shadow 'org.spongepowered:spongeapi:8.1.0'

    // Kyori Adventure
    shadow 'net.kyori:adventure-text-serializer-legacy:4.10.1'
    shadow 'net.kyori:adventure-text-serializer-gson:4.10.1'

    // Protocol Control
    //shadow 'com.ichorpowered:protocolcontrol:0.0.3'

    testImplementation project(':api')
    testImplementation project(':common')
    testImplementation 'net.impactdev.impactor:api:4.0.0-SNAPSHOT'
    testImplementation 'org.spongepowered:configurate-core:4.1.2'
    testImplementation 'org.spongepowered:configurate-gson:4.1.2'
    testImplementation 'org.spongepowered:configurate-yaml:4.1.2'
    testImplementation 'org.spongepowered:configurate-hocon:4.1.2'
    testImplementation 'com.github.ben-manes.caffeine:caffeine:2.8.2'
    testImplementation 'net.kyori:adventure-api:4.10.1'
}

shadowJar {
    archiveBaseName.set("gts-sponge")
    archiveClassifier.set("")
    archiveVersion.set("")
    archiveExtension.set("jarinjar")

    dependencies {
        include project(':common')
        include project(':sponge')
    }

    relocate ('com.github.benmanes.caffeine', 'net.impactdev.impactor.relocations.caffeine')
    relocate ('io.github.classgraph', 'net.impactdev.impactor.relocations.classgraph')
    relocate ('org.mariuszgromada.math.mxparser', 'net.impactdev.impactor.relocations.mxparser')
    relocate ('redis.clients.jedis', 'net.impactdev.impactor.relocations.jedis')
    relocate ('org.apache.commons.pool2', 'net.impactdev.impactor.relocations.commonspool2')
}

reobf {
    shadowJar {}
}

blossom {
    replaceToken '@version@', version
    replaceToken '@git_commit@', gitHash.commit
    replaceToken '@description@', 'A player controlled market system'
}

jar.dependsOn(gitHash)
build.dependsOn reobfShadowJar