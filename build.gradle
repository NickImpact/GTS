buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

plugins {
    id 'com.qixalite.spongestart' version '1.6.2'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

group = 'me.nickimpact.gts'
version = '5.2.1'

apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    ext {
        sponge = "7.1.5"
        spigot = "1.12.2"
    }

    version = '5.2.1'
    group = 'me.nickimpact.gts'

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven {
            url "https://nexus.unboundnetwork.net/repository/unbound-public/"
        }
    }

    jar {
        exclude 'Impactor*.jar'
    }

    shadowJar {
        exclude 'Impactor*.jar'
    }
    
    dependencies {
        shadow "com.nickimpact.impactor:spigot:2.1.0"
        shadow "com.nickimpact.impactor:sponge:2.1.0"
        shadow "org.projectlombok:lombok:1.18.4"
        shadow 'com.google.guava:guava:27.1-jre'
        shadow 'com.google.code.gson:gson:2.8.5'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task copyJars(type: Copy) {
    dependsOn subprojects.build
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/allJars"
    exclude '*-all.jar'
    exclude '*-sources.jar'
    exclude 'api*.jar'
    exclude 'common*.jar'
    exclude 'spigot*.jar'
    exclude 'sponge*.jar'
    exclude 'plugin*.jar'
    exclude 'reforged*.jar'
    exclude 'generations*.jar'
}

publishing {
    repositories {
        maven {
            name = "Unbound-Public"
            url = "https://nexus.unboundnetwork.net/repository/unbound-public/"
            credentials {
                username project.hasProperty('nexus-username') ? project.property('nexus-username') : ''
                password project.hasProperty('nexus-password') ? project.property('nexus-password') : ''
            }
        }
    }

    publications {
        api(MavenPublication) {
            def project = project(':api')
            from project.components.java
            groupId project.group
            artifactId "api"
            version project.version
        }
        common(MavenPublication) {
            def project = project(':common')
            from project.components.java
            groupId project.group
            artifactId "common"
            version project.version
        }
        sponge(MavenPublication) {
            def project = project(':sponge')
            from project.components.java
            groupId project.group
            artifactId "sponge"
            version project.version
        }
        pluginSponge(MavenPublication) {
            def project = project(':plugin-sponge')
            from project.components.java
            groupId project.group
            artifactId "plugin-sponge"
            version project.version
        }
        reforgedSponge(MavenPublication) {
            def project = project(':reforged-sponge')
            from project.components.java
            groupId project.group
            artifactId "reforged-sponge"
            version project.version
        }
    }
}

build.dependsOn shadowJar
build.dependsOn copyJars